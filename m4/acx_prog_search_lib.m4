# acx_prog_search_lib.m4: A m4 macros that search the adequate library among a
# library list to link a program.
#
# Copyright Â© 2009 Frederic Chateau <frederic.chateau@cea.fr>
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#
# As a special exception to the GNU General Public License, if you
# distribute this file as part of a program that contains a
# configuration script generated by Autoconf, you may include it under
# the same distribution terms that you use for the rest of that program.
#

#
# SYNOPSIS
#	ACX_PROG_SEARCH_LIB(input, libraries, [action-if-found], [action-if-not-found])
#
# DESCRIPTION
#	Search for the library necessary to successfully link a simple program using
#	that library.
#
# PARAMETERS
#	input
#		The input for the compiler and the linker. You may use AC_LANG_SOURCE or
#		AC_LANG_PROGRAM to generate it.
#	libraries
#		A list of whitespace separated library names.
#	action-if-found
#		The code to execute when a library allowed to link the program.
#		The $found_lib variable is bound to the name of that library,
#		and -l$found_lib is added to LIBS.
#	action-if-not-found
#		The code to execute when no library allowed to link the program.
#
AC_DEFUN([ACX_PROG_SEARCH_LIB],
[
	saved_LIBS="$LIBS"
	has_found_lib="no"
	m4_foreach_w([tested_lib], [$2], 
	[
		if test "$has_found_lib" != "yes"
		then
			tested_library=tested_lib
			LIBS="-l$tested_library $saved_LIBS"
			AC_LINK_IFELSE([$1],
			[
				has_found_lib="yes"
				found_lib="$tested_library"
			])
		fi
	])
	if test "$has_found_lib" = "yes"
	then
		$3
	else
		$4
	fi
])
