# ax_build_modes.m4: An m4 macro to easily select build modes like debug
# or release, profiling or not, etc.
#
# Copyright Â© 2008 Frederic Chateau <frederic.chateau@cea.fr>
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#
# As a special exception to the GNU General Public License, if you
# distribute this file as part of a program that contains a
# configuration script generated by Autoconf, you may include it under
# the same distribution terms that you use for the rest of that program.
#

#
# SYNOPSIS
#	AX_DEBUG_OR_RELEASE_BUILD
#
# DESCRIPTION
#	Provides a configure switch to enable debug or release builds.
#	Options:
#	--enable-debug=(yes|no)
#		Indicates whether to build the package using debug mode, or not.
#
#	This macro calls:
#		AC_SUBST(DISTCHECK_CONFIGURE_FLAGS)
#

AC_DEFUN([AX_DEBUG_OR_RELEASE_BUILD],
[
AC_ARG_ENABLE([debug],
   AS_HELP_STRING([--enable-debug], [Turn on debugging]),
   [case "${enableval}" in
      yes) enable_debug=yes ;;
      no)  enable_debug=no ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
   esac],
   [enable_debug=no]
)
if test "$enable_debug" = "yes"
then
    CXXFLAGS="-g3 -O0 ${CXXFLAGS}"
    CFLAGS="-g3 -O0 ${CFLAGS}"
	CPPFLAGS="-UNDEBUG ${CPPFLAGS}"
	DISTCHECK_CONFIGURE_FLAGS="$DISTCHECK_CONFIGURE_FLAGS --enable-debug"
	FULL_SUFFIX="${FULL_SUFFIX}d" # this suffix may be composted with other suffixes
	DEBUG_SUFFIX="-d" # this suffix only describe the debug switch
else
    CXXFLAGS="-g0 -O3 ${CXXFLAGS}"
    CFLAGS="-g0 -O3 ${CFLAGS}"
    CPPFLAGS="-DNDEBUG ${CPPFLAGS}"
fi
AC_SUBST([DISTCHECK_CONFIGURE_FLAGS])
])

#
# SYNOPSIS
#	AX_PROFILING_MODE
#
# DESCRIPTION
#	Provides a configure switch to enable profiling.
#	Options:
#	--enable-profiling=(yes|no)
#		Indicates whether to build the package using profiling mode, or not.
#
#	This macro calls:
#
AC_DEFUN([AX_PROFILING_MODE],
[
AC_ARG_ENABLE([profiling],
   AS_HELP_STRING([--enable-profiling], [Turn on profiling]),
   [case "${enableval}" in
      yes) enable_profiling=yes ;;
      no)  enable_profiling=no ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-profiling]) ;;
   esac],
   [enable_profiling=false]
)
if test "$enable_profiling" = "false"
then
	CXXFLAGS="-pg ${CXXFLAGS}"
    CFLAGS="-pg ${CFLAGS}"
    FULL_SUFFIX="${FULL_SUFFIX}p"
    PROFILE_SUFFIX="-p"
fi
])


AC_DEFUN([AX_DEBUG_AND_RELEASE_BUILD],
[
AC_ARG_ENABLE([debug],
   AS_HELP_STRING([--disable-debug], [Turn off debug build]),
   [case "${enableval}" in
      yes) enable_debug=yes ;;
      no)  enable_debug=no ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
   esac],
   [enable_debug=yes]
)
if test "$enable_debug" = "yes"
then
    DEBUG_CXXFLAGS="-g3 -O0"
    DEBUG_CFLAGS="-g3 -O0"
	DEBUG_CPPFLAGS="-UNDEBUG"
fi
AC_ARG_ENABLE([release],
   AS_HELP_STRING([--disable-release], [Turn off release build]),
   [case "${enableval}" in
      yes) enable_release=yes ;;
      no)  enable_release=no ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-release]) ;;
   esac],
   [enable_release=yes]
)
if test "$enable_release" = "yes"
then
    RELEASE_CXXFLAGS="-g0 -O3"
    RELEASE_CFLAGS="-g0 -O3"
    RELEASE_CPPFLAGS="-DNDEBUG"
fi
DISTCHECK_CONFIGURE_FLAGS="$DISTCHECK_CONFIGURE_FLAGS --enable-debug=$enable_debug --enable-release=$enable_release"
AC_SUBST([DISTCHECK_CONFIGURE_FLAGS])
AC_SUBST([DEBUG_CXXFLAGS])
AC_SUBST([DEBUG_CFLAGS])
AC_SUBST([DEBUG_CPPFLAGS])
AC_SUBST([RELEASE_CXXFLAGS])
AC_SUBST([RELEASE_CFLAGS])
AC_SUBST([RELEASE_CPPFLAGS])
AM_CONDITIONAL([DEBUG], [test "$enable_debug" = "yes"])
AM_CONDITIONAL([RELEASE], [test "$enable_release" = "yes"])
])
